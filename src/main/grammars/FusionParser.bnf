{
    classHeader='copyrightHeader.java'

    generateTokenAccessors=true
    parserClass="de.vette.idea.neos.lang.fusion.parser.FusionParser"
    parserUtilClass="de.vette.idea.neos.lang.fusion.parser.FusionParserUtil"

    implements='de.vette.idea.neos.lang.fusion.psi.FusionCompositeElement'
    extends="de.vette.idea.neos.lang.fusion.psi.impl.FusionCompositeElementImpl"

    psiClassPrefix="Fusion"
    psiImplClassSuffix="Impl"
    psiPackage="de.vette.idea.neos.lang.fusion.psi"
    psiImplPackage="de.vette.idea.neos.lang.fusion.psi.impl"

    elementTypeHolderClass="de.vette.idea.neos.lang.fusion.psi.FusionTypes"
    elementTypeClass="de.vette.idea.neos.lang.fusion.psi.FusionElementType"
    tokenTypeClass="de.vette.idea.neos.lang.fusion.psi.FusionTokenType"
}

FusionFile ::= item_*
private item_ ::= (namespace_declaration | include_declaration | property | SINGLE_LINE_COMMENT | C_STYLE_COMMENT | DOC_COMMENT) {
    recoverWhile="item_recovery"
}

private item_recovery ::= !(PROTOTYPE_KEYWORD|PATH_PART|EOF|VALUE_STRING_QUOTE|RIGHT_BRACE|LEFT_BRACE|INCLUDE_KEYWORD|NAMESPACE_KEYWORD|META_PROPERTY_KEYWORD|SINGLE_LINE_COMMENT|C_STYLE_COMMENT|DOC_COMMENT)

inner block ::= LEFT_BRACE [item_ *] RIGHT_BRACE {pin=1}

namespace_declaration ::= NAMESPACE_KEYWORD NAMESPACE_SEPARATOR alias NAMESPACE_ALIAS_SEPARATOR namespace {
    pin=1
    implements = "de.vette.idea.neos.lang.fusion.psi.FusionNamedElement"
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.ext.FusionNamespaceDeclarationImplMixin"
    stubClass = "de.vette.idea.neos.lang.fusion.stubs.FusionNamespaceDeclarationStub"
    elementTypeFactory = "de.vette.idea.neos.lang.fusion.stubs.StubElementTypeFactory.factory"
}

alias ::= NAMESPACE_ALIAS
namespace ::= PACKAGE_KEY

include_declaration ::= INCLUDE_KEYWORD INCLUDE_SEPARATOR (resource|INCLUDE_PATH) {pin=1}
resource ::= RESOURCE_KEYWORD RESOURCE_PATH {pin=1}

private property ::= path (property_block|property_deletion|property_copy|property_assignment)
left property_block ::= block
left property_deletion ::= UNSET_OPERATOR
left property_assignment ::= ASSIGNMENT_OPERATOR (VALUE_BOOLEAN|VALUE_NULL|VALUE_NUMBER|expression|value_dsl|value_string_line|prototype_instance) {pin=1}
left property_copy ::= COPY_OPERATOR (VALUE_BOOLEAN|VALUE_NULL|VALUE_NUMBER|value_string_line|prototype_instance|(prototype_signature [block])) {
    pin=1
    implements = [ "de.vette.idea.neos.lang.fusion.psi.FusionCompositeElement"; "de.vette.idea.neos.lang.fusion.psi.ext.FusionPropertyCopyMixin"]
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.ext.FusionPropertyCopyImplMixin"
}

expression ::= EXPRESSION_KEYWORD EEL_LEFT_BRACE expression_content EEL_RIGHT_BRACE {pin=1}

expression_content ::= EEL_CONTENT {
    implements = ["de.vette.idea.neos.lang.fusion.psi.FusionCompositeElement"; "com.intellij.psi.tree.ILazyParseableElementTypeBase"]
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.FusionLazyParsableElement"
}

type ::= [OBJECT_TYPE_NAMESPACE OBJECT_TYPE_SEPARATOR] UNQUALIFIED_TYPE {
    pin=2
    implements = "de.vette.idea.neos.lang.fusion.psi.FusionNamedElement"
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.ext.FusionTypeImplMixin"
}

prototype_instance ::= type [block] {
    pin=1
    implements = [ "de.vette.idea.neos.lang.fusion.psi.FusionReferenceElement" ]
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.ext.FusionPrototypeInstanceImplMixin"
}

path ::= [PATH_SEPARATOR] (PATH_PART|meta_property|prototype_signature|value_string_single_line) (PATH_SEPARATOR (PATH_PART|prototype_signature|meta_property))* {
    pin=2
    implements = [ "de.vette.idea.neos.lang.fusion.psi.FusionNamedElement";  "de.vette.idea.neos.lang.fusion.psi.ext.FusionPathMixin"]
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.ext.FusionPathImplMixin"
}

meta_property ::= META_PROPERTY_KEYWORD META_PROPERTY_NAME {pin=1}
prototype_signature ::= PROTOTYPE_KEYWORD LEFT_PAREN type RIGHT_PAREN {
    pin=1
    implements = [ "de.vette.idea.neos.lang.fusion.psi.FusionReferenceElement"; "de.vette.idea.neos.lang.fusion.psi.ext.FusionPrototypeSignature" ]
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.ext.FusionPrototypeSignatureImplMixin"
    stubClass = "de.vette.idea.neos.lang.fusion.stubs.FusionPrototypeSignatureStub"
    elementTypeFactory = "de.vette.idea.neos.lang.fusion.stubs.StubElementTypeFactory.factory"
}

value_dsl ::= DSL_IDENTIFIER BACKTICK value_dsl_content BACKTICK {
    pin=1
}

value_dsl_content ::= (VALUE_STRING | CRLF | ESCAPED_BACKTICK)* {
    implements = [ "com.intellij.psi.PsiLanguageInjectionHost" ]
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.ext.FusionValueDslContentImplMixin"
}

value_string_line ::= VALUE_STRING_QUOTE value_string_line_content VALUE_STRING_QUOTE {
    pin=1
    implements = [ "de.vette.idea.neos.lang.fusion.psi.FusionReferenceElement" ]
    mixin = "de.vette.idea.neos.lang.fusion.psi.impl.ext.FusionValueStringLineImplMixin"
}

value_string_line_content ::= (VALUE_STRING | CRLF | VALUE_STRING_ESCAPED_QUOTE)*
value_string_single_line ::= VALUE_STRING_QUOTE (VALUE_STRING | VALUE_STRING_ESCAPED_QUOTE)* VALUE_STRING_QUOTE {pin=1}